plugins {
    id 'java'
}

repositories {
    mavenCentral()
}

configurations { antlr4 }

sourceSets {
    main { java { srcDir 'build/generated-src/java' } }
}

dependencies {
    implementation 'org.antlr:antlr4-runtime:4.9.2'
    antlr4 "org.antlr:antlr4:4.9.2"
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
}

test {
    useJUnitPlatform()
}

task generateSources(type: JavaExec) {
    inputs.dir file('src/main/antlr')
    outputs.dir file('build/generated-src/java')

    mainClass = 'org.antlr.v4.Tool'
    classpath = configurations.antlr4

    args = ["-o", 'build/generated-src/java', fileTree('src/main/antlr').include('*.g4').files].flatten()

}

compileJava.dependsOn generateSources

defaultTasks 'clean', 'build'

